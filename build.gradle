plugins {
    id 'idea'
    id 'org.shipkit.shipkit-auto-version' version '2.0.7'
}

allprojects { project ->
    group 'io.github.joke'
    description 'deep mocking for the spock framework'
    version rootProject.version

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaBasePlugin).configureEach {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }
    }

    plugins.withType(MavenPublishPlugin) {
        project.plugins.apply 'signing'

        plugins.withType(JavaPlugin).configureEach {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        }

        plugins.withType(SigningPlugin).configureEach {
            afterEvaluate {
                signing {
                    required {
                        gradle.taskGraph.hasTask('publish')
                    }
                    useGpgCmd()
                    sign publishing.publications
                }
            }
        }

        publishing {
            repositories {
                maven {
                    name = 'OSSRH'
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials(PasswordCredentials)
                }
                maven {
                    name = 'GitHubPackages'
                    url = uri("https://maven.pkg.github.com/joke/${project.name}")
                    credentials(PasswordCredentials)
                }
            }
            afterEvaluate {
                tasks.withType(AbstractPublishToMaven)*.dependsOn(check)
                publications.each { publication ->
                    publication.pom {
                        name = project.name
                        description = project.description
                        url = "https://github.com/joke/${project.name}"
                        licenses {
                            license {
                                name = 'Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }
                        developers {
                            developer {
                                id = 'joke'
                                name = 'Joke de Buhr'
                                email = 'joke@xckk.de'
                            }
                        }
                        scm {
                            connection = "scm:git:git://github.com/joke/${project.name}.git"
                            developerConnection = "scm:git:git://github.com/joke/${project.name}.git"
                            url = "http://github.com/joke/${project.name}/tree/main"
                        }
                    }
                }
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '8.6'
}
